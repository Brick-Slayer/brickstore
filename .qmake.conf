
SOURCE_DIR = $$PWD
BUILD_DIR = $$shadowed($$PWD)

VERSION = $$cat($$SOURCE_DIR/VERSION)
VERSION_SPLIT = $$split(VERSION, .)
VERSION_MAJOR = $$member(VERSION_SPLIT, 0)
VERSION_MINOR = $$member(VERSION_SPLIT, 1)

BUILD_NUMBER = $$(BUILD_NUMBER)
isEmpty(BUILD_NUMBER) : BUILD_NUMBER = 0

VERSION = "$${VERSION_MAJOR}.$${VERSION_MINOR}.$${BUILD_NUMBER}"

win32:BUILD_USER = $$(USERNAME)
unix:BUILD_USER = $$(USER)
win32:BUILD_HOST = $$(COMPUTERNAME)
unix:BUILD_HOST = $$system(hostname)

GIT_VERSION = $$cat($$SOURCE_DIR/.tag)
GIT_VERSION = $$replace(GIT_VERSION, "^\\$Format:.*\\$", "")
isEmpty(GIT_VERSION) : exists($$SOURCE_DIR/.git) {
  if (win32 : system(git --version >NUL)) | if (unix : system(git --version >/dev/null)) {
    GIT_VERSION=$$system(git --git-dir $$SOURCE_DIR/.git --work-tree $$SOURCE_DIR  describe --always --tags --dirty)
  }
}
isEmpty(GIT_VERSION) : GIT_VERSION="unknown"
